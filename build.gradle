import java.text.SimpleDateFormat

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
}

group = 'com.pwdim'


def getCommitHash = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
            ignoreExitValue = true
        }
        String result = stdout.toString().trim()
        return result.isEmpty() ? "unknown" : result
    } catch (Exception ignored) {
        return "noscm"
    }
}

def determinePatchVersion = {
    try {
        def tagInfo = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = tagInfo
            ignoreExitValue = true
        }
        String output = tagInfo.toString().trim()
        if (!output || !output.contains('-')) return "0"
        return output.split("-")[1]
    } catch (Exception ignored) {
        return "0"
    }
}


String major = '1'
String minor = '0'
String patch = determinePatchVersion()
String hash = getCommitHash()

version = "${major}.${minor}.${patch}-${hash}".toString()

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "spigot"
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = "glaremasters"
        url = "https://repo.glaremasters.me/repository/concuncan/"
    }
}

dependencies {
    implementation("fr.mrmicky:fastboard:2.1.2")
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly name: 'spigotNMS'
    compileOnly 'com.grinderwolf:slimeworldmanager-api:2.2.1'
}

shadowJar {
    relocate 'fr.mrmicky.fastboard', 'com.pwdim.fastboard'
    archiveClassifier.set('')
}

tasks.build.dependsOn tasks.shadowJar

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}